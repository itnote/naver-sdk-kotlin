buildscript {
    ext.kotlin_version = '1.1.3'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'kotlin-lib'
group 'org.naver.sdk'
version '0.2.0'
description 'The unofficial NAVER SDK for Kotlin'

allprojects {
    repositories {
        jcenter()
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "com.squareup.okhttp3:okhttp:3.8.1"
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    compile 'com.google.code.gson:gson:2.8.1'
    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

test {
    testLogging {
        showExceptions = true
        showStackTraces = true
        exceptionFormat = "full"
        events = ["failed", "skipped"]
        showStandardStreams = true
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'maven'
        name = 'naver-sdk-kotlin'
        userOrg = user
        desc = project.description
        websiteUrl = 'https://github.com/kimsuelim/naver-sdk-kotlin'
        issueTrackerUrl = 'https://github.com/kimsuelim/naver-sdk-kotlin/issues'
        vcsUrl = 'https://github.com/kimsuelim/naver-sdk-kotlin.git'
        licenses = ['MIT']
        labels = ['kotlin', 'naver']
        publicDownloadNumbers = true
        githubRepo = 'kimsuelim/naver-sdk-kotlin'
        githubReleaseNotesFile = 'CHANGELOG.md'

        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
        }
    }
}
